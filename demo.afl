// Define parameters
// Note that the parameter names are prepended with the StrategyCode, which should be unique
// across the strategies that you run. This is because the parameters are global across any
// strategies you're running in Amibroker. This can cause collisions when you have the same
// parameter name in use across more than one strategy.
StrategyCode = "DEMO";
TARGET_R = Param( StrategyCode + " Target R", 3.5, 0, 100, 0.1 );
RISK = Param( StrategyCode + " Risk Dollars", 100, 0, 10000, 1 );
MinGapPercent = Param( StrategyCode + " Min Gap Percent", 3.5, -100, 100, 0.1 );
FirstTradeTime = Param( StrategyCode + " First Trade Time", 5, 0, 390, 1 );
LatestTradeTime = Param( StrategyCode + " Latest Trade Time", 90, 0, 390, 1 );
TimedExitTime = Param( StrategyCode + " Timed Exit Time", 380, 0, 390, 1 );
Exclude_Warrants = ParamToggle( StrategyCode + " Exclude Warrants", "No|Yes", 0 );
Write_Signal_File = ParamToggle( StrategyCode + " Write Signal File", "No|Yes", 0 );
IB_Time_Stop = ParamStr( StrategyCode + "Time Stop", "15:56:30 EST");
IB_Account_Number = ParamStr( StrategyCode + "IB Account", "UXXXXXX");
IB_Trading_Enabled = ParamToggle( StrategyCode + " IB Trading Enabled", "No|Yes", 0 );
IB_Actually_Transmit = ParamToggle( StrategyCode + " IB Actually Transmit", "No|Yes", 0 );

Is_Warrant = ( StrLen( Name() ) == 5 AND StrMid( Name(), StrLen(Name())-1, 1 ) == "W" )
        OR ( StrFind( Name(), "+" ) > 0 );

#include "e:\\github_amibrokerti\\shared_variables.afl";

// Trigger logic here
BasicRequirements = TodayGapPercent >= MinGapPercent AND ( NOT Exclude_Warrants OR NOT Is_Warrant );
OnTodaysWatchlist = HighestSince( newday, BasicRequirements, 1 );
Trigger = OnTodaysWatchlist && HOD > Ref( HOD, -1 ) && MinutesSinceOpen >= FirstTradeTime && MinutesSinceOpen < LatestTradeTime;
Invalid = 0; //V < 1000000; // include your bad filter threshold


Buy = 0;
BuyPrice = 0;
Short = 0;
ShortPrice = 0;
Sell = 0;
SellPrice = 0;
Cover = 0;
CoverPrice = 0;
PositionSize = 0;

MinBarCount = 0;
MaxBarCount = LastValue( bi );
in_trade = 0;
invalid_today = 0;

for( i = MinBarCount; i < MaxBarCount; i++ )
{
	if( premarketopen[i] )
	{
		invalid_today = 0;
	}

    if( in_trade )
    {
        exit_reason = "";

        // check for time stop
        if( MinutesSinceOpen[i] >= TimedExitTime || NextIsEOD[i] )
        {
            Sell[i] = 1;
            SellPrice[i] = O[i];
            exit_reason = "Timed Exit";
            in_trade = 0;
        }

        if( L[i] <= stop_price && !Buy[i] )
        {
            Sell[i] = 1;
            SellPrice[i] = stop_price;
            exit_reason = "Initial Stop";
            in_trade = 0;
        }

        // check for target
        if( H[i] > target_price && !Buy[i] )
        {
            Sell[i] = 1;
            SellPrice[i] = target_price;
            exit_reason = "Target";
            in_trade = 0;
        }

        if( exit_reason != "" )
        {
            metric_value = exit_reason
                + "," + TodayGap[i] + "," + YesterdayDojiness[i] + "," + trd_volume_today
                // START_METRIC_AREA
                // END_METRIC_AREA
                ;
            StaticVarSetText( "Ex" + Name() + DateTimeToStr( dt[i] ), metric_value );
        }
    }

    if( Trigger[i] AND NOT in_trade AND NOT invalid_today )
    {
        SignalBarIndex = i - 1;
        if( Invalid[SIgnalBarIndex] )
        {
			invalid_today = 1;
		}
		else
		{
			Buy[i] = 1;
			BuyPrice[i] = HOD[SignalBarIndex] + 0.01;
			stop_price = LOD[SignalBarIndex] - 0.01;
			initial_stop_distance = abs( BuyPrice[i] - stop_price );
			target_price = BuyPrice[i] + ( initial_stop_distance * TARGET_R );
			PositionSize[i] = ( RISK / initial_stop_distance ) * BuyPrice[i];
			ShareSize = PositionSize[i] / BuyPrice[i];

			// create variables for any columns to keep track of
			// then append them to the metric_value lines above
			// then add lines for them in the
			in_trade = 1;
			trd_entry_bar_index = bi[i];

			// define variables for metrics
			trd_hod = HOD[SignalBarIndex];
			trd_lod = LOD[SignalBarIndex];
			trd_volume_today = VolumeToday[SignalBarIndex];
			// START_SIGNAL_AREA
			// END_SIGNAL_AREA
		}
    }
}

// Exploration settings
SetOption("nodefaultcolumns", True);
// Filter = 1;
Filter = Trigger;
AddTextColumn(Name(), "Sym", Null, colorDefault, colorDefault, 50);
AddColumn( DateTime(), "D/T", formatDateTime, colorDefault, colorDefault, 150);
AddColumn( MinutesSinceOpen, "MinutesSinceOpen", 1.0, colorDefault, IIf( MinutesSinceOpen > 0, GetGradient( colorWhite, colorBlue, 0, 390, MinutesSinceOpen ), GetGradient( colorRed, colorWhite, -330, 0, MinutesSinceOpen )) );
AddColumn( OnTodaysWatchlist, "OnTodaysWatchlist", 1.0 );
AddColumn( Trigger, "Trigger", 1.0 );
AddColumn( C, "C", 1.2, colorDefault, GetGradient( colorWhite, colorGreen, 0, 50, C ) );
AddColumn( Buy, "Buy", 1.0 );
AddColumn( Sell, "Sell", 1.0 );
AddColumn( HOD, "HOD", 1.2 );
AddColumn( Ref( HOD, -1 ), "HOD-1", 1.2, colorDefault, IIf( HOD > Ref( HOD, -1 ), colorGreen, colorDefault ) );
AddColumn( H, "H", 1.2 );
AddColumn( V, "V", 1.2 );
AddColumn( MK_Vol, "MK_Vol", 1.0 );
AddColumn( MK_RV, "RV", 1.2 );
AddColumn( MK_TV, "VolumeToday", 1.0 );
AddColumn( MK_VWV, "MK_VWV", 1.3 );

// Controls the sort in the exploration results
SetSortColumns(-5);

// Custom backtest code - this actually adds the columns to the trades list
SetCustomBacktestProc( "" );

if( Status( "action" ) == actionPortfolio )
{
    bo = GetBacktesterObject();
    bo.Backtest( 1 ); // run default backtest procedure

    for( trade = bo.GetFirstTrade(); trade; trade = bo.GetNextTrade() )
    {
        // this is the metric_value from the exit logic above
        trade_metric_value = StaticVarGetText( "Ex" + trade.Symbol + DateTimeToStr( trade.ExitDateTime ) );
        exit_reason = StrExtract( trade_metric_value, 0 );
        trade.AddCustomMetric( "Exit Reason", exit_reason );
        gap_percent = StrToNum( StrExtract( trade_metric_value, 1 ) );
        trade.AddCustomMetric( "Gap Percent", gap_percent );
        yesterday_dojiness = StrToNum( StrExtract( trade_metric_value, 2 ) );
        trade.AddCustomMetric( "Yest Dojiness", yesterday_dojiness );
        today_volume = StrToNum( StrExtract( trade_metric_value, 3 ) );
        trade.AddCustomMetric( "Volume Today", today_volume, 0 );
        // START_BACKTEST_METRIC_AREA
		// END_BACKTEST_METRIC_AREA
    }

    bo.ListTrades();
}
